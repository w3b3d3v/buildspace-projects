üòà Escolha um vencedor aleatoriamente
-----------------------

Ent√£o, agora, nosso c√≥digo est√° configurado para dar 0.0001 ETH a todo tchauzinho! Nosso contrato ficar√° sem dinheiro muito r√°pido, e ent√£o a divers√£o acaba e precisar√≠amos adicionar mais fundos ao nosso contrato. Nesta li√ß√£o, mostrarei como:

1\. Escolher **aleatoriamente** um vencedor.

2\. Criar um mecanismo de **cooldown** (esfriamento) para evitar que as pessoas enviem spam para voc√™ na tentativa de ganhar o pr√™mio ou de incomod√°-lo.

Vamos fazer primeiro a parte do vencedor aleat√≥rio!

Ent√£o, gerar um n√∫mero aleat√≥rio em contratos inteligentes √© amplamente conhecido como um **problema dif√≠cil**.

Por qu√™? Bem, pense em como um n√∫mero aleat√≥rio √© gerado normalmente. Quando voc√™ gera um random normalmente em um programa, **vai pegar v√°rios n√∫meros diferentes do seu computador como fonte de aleatoriedade** como: a velocidade das ventoinhas, a temperatura da CPU, o n√∫mero de vezes que voc√™ pressionou "L" √†s 15h52 desde que comprou o computador, a velocidade da sua internet e muitas outras coisas que s√£o dif√≠ceis de controlar. Ele pega **todos** esses n√∫meros que s√£o "aleat√≥rios" e os re√∫ne em um algoritmo que gera um n√∫mero que parece ser a melhor tentativa de um n√∫mero realmente "aleat√≥rio". Faz sentido?

Na blockchain, n√£o h√° **quase nenhuma fonte de aleatoriedade**. Tudo o que o contrato v√™, o p√∫blico v√™. Por causa disso, algu√©m poderia manipular o sistema apenas olhando para o contrato inteligente, vendo em que #s ele depende para aleatoriedade e, em seguida, a pessoa poderia fornecer os n√∫meros exatos de que precisa para vencer.

Vamos conferir o c√≥digo abaixo üòä

```solidity
// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.0;

import "hardhat/console.sol";

contract WavePortal {
    uint256 totalWaves;

    /*
     * Utilizaremos isso abaixo para gerar um n√∫mero rand√¥mico
     */
    uint256 private seed;

    event NewWave(address indexed from, uint256 timestamp, string message);
    /*
     * Crio um struct Wave.
     * Um struct √© basicamente um tipo de dados customizado onde n√≥s podemos customizar o que queremos armazenar dentro dele
     */
    struct Wave {
        address waver; // Endere√ßo do usu√°rio que deu tchauzinho
        string message; // Mensagem que o usu√°rio envio
        uint256 timestamp; // Data/hora de quando o usu√°rio tchauzinhou.
    }

    Wave[] waves;

    constructor() payable {
        console.log("Contrato no ar!");
        /*
         * Define a semente inicial
         */
        seed = (block.timestamp + block.difficulty) % 100;
    }

    function wave(string memory _message) public {
        totalWaves += 1;
        console.log("%s tchauzinhou!", msg.sender);

        /*
         * Aqui √© onde eu efetivamenet armazeno o tchauzinho no array.
         */
        waves.push(Wave(msg.sender, _message, block.timestamp));

        /*
         * Gera uma nova semente para o pr√≥ximo que mandar um tchauzinho
         */
        seed = (block.difficulty + block.timestamp + seed) % 100;

        console.log("# randomico gerado: %d", seed);

        /*
         * D√° 50%  de chance do usu√°rio ganhar o pr√™mio.
         */
        if (seed <= 50) {
            console.log("%s ganhou!", msg.sender);

            /*
             * O mesmo c√≥digo que t√≠nhamos anteriormente para enviar o pr√™mio.
             */
            uint256 prizeAmount = 0.0001 ether;
            require(
                prizeAmount <= address(this).balance,
                "Tentando sacar mais dinheiro que o contrato possui."
            );
            (bool success, ) = (msg.sender).call{value: prizeAmount}("");
            require(success, "Falhou em sacar dinheiro do contrato.");
        }

        /*
         * Eu adicionei algo novo aqui. Use o Google para tentar entender o que √© e depois me conte o que aprendeu em #general-chill-chat
         */
        emit NewWave(msg.sender, block.timestamp, _message);
    }

    /*
     * Adicionei uma fun√ß√£o getAllWaves que retornar√° os tchauzinhos.
     * Isso permitir√° recuperar os tchauzinhos a partir do nosso site!
     */
    function getAllWaves() public view returns (Wave[] memory) {
        return waves;
    }

    function getTotalWaves() public view returns (uint256) {
        return totalWaves;
    }
}
```

Aqui, eu pego dois n√∫meros dados a mim pelo Solidity, `block.difficulty` e `block.timestamp` e os combino para criar um n√∫mero aleat√≥rio. `block.difficulty` informa aos mineradores o qu√£o dif√≠cil o bloco ser√° para minerar com base nas transa√ß√µes no bloco. Os blocos ficam mais dif√≠ceis por v√°rios motivos, mas, principalmente, ficam mais dif√≠ceis quando h√° mais transa√ß√µes no bloco (alguns mineradores preferem blocos mais f√°ceis, mas pagam menos). `block.timestamp` √© apenas o timestamp Unix que o bloco est√° sendo processado.

Esses #s s√£o *bastante* aleat√≥rios. Mas, tecnicamente, tanto o `block.difficulty` quanto o `block.timestamp` podem ser controlados por um invasor sofisticado.

Para tornar isso mais dif√≠cil, crio uma vari√°vel `seed` que mudar√° essencialmente toda vez que um usu√°rio enviar um novo tchauzinho. Ent√£o, combino todas essas tr√™s vari√°veis para gerar uma nova semente aleat√≥ria. Ent√£o eu apenas fa√ßo `% 100`, o que garantir√° que o n√∫mero seja reduzido para um intervalo entre 0 e 100.

√â isso! Ent√£o eu apenas escrevo uma simples declara√ß√£o IF para ver se a semente √© menor ou igual a 50, se for -- ent√£o a pessoa que "mandou o tchau" ganha o pr√™mio! Ent√£o, isso significa que o "mandador de tchau" tem 50% de chance de ganhar desde que escrevemos `seed <= 50`. Voc√™ pode mudar isso para o que quiser üòä Acabei de fazer 50% porque √© mais f√°cil testar assim!!

√â importante ver que um ataque poderia tecnicamente enganar seu sistema se eles realmente quisessem. Seria muito dif√≠cil. Existem outras maneiras de gerar n√∫meros aleat√≥rios no blockchain, mas o Solidity n√£o nos fornece nada confi√°vel porque n√£o pode! Todos os #s que nosso contrato pode acessar s√£o p√∫blicos e *nunca* verdadeiramente aleat√≥rios.

Realmente, este √© um dos pontos fortes do blockchain. Mas, pode ser um pouco chato para algum aplicativo como o nosso!

De qualquer forma, ningu√©m vai atacar nosso pequeno aplicativo, mas quero que voc√™ saiba tudo isso quando estiver criando um dApp que tenha milh√µes de usu√°rios!

Teste!
-------

Vamos garantir que funcione! Aqui est√° meu `run.js` atualizado. Neste caso, eu s√≥ quero ter certeza de que o saldo do contrato muda no caso em que a pessoa que deu tchauzinhou ganhou!

```javascript
const main = async () => {
    const waveContract = await hre.ethers.deployContract("WavePortal", { value: hre.ethers.parseEther('0.1') });
    await waveContract.waitForDeployment();
    console.log("Contract deployed to:", waveContract.target);

    /*
    * Consulta saldo do contrato
    */
    let contractBalance = await hre.ethers.provider.getBalance(
        waveContract.target
    );
    console.log(
        "Saldo do contrato:",
        hre.ethers.formatEther(contractBalance)
    );

    /*
     * Vamos tentar mandar um tchauzinho 2 vezes agora
     */
    const waveTxn = await waveContract.wave("tchauzinho #1");
    await waveTxn.wait();

    const waveTxn2 = await waveContract.wave("tchauzinho #2");
    await waveTxn2.wait();

    /*
    * Recupera o saldo do contrato para verificar o que aconteceu!
    */
    contractBalance = await hre.ethers.provider.getBalance(waveContract.target);
    console.log(
        "Saldo do contrato:",
        hre.ethers.formatEther(contractBalance)
    );

    let allWaves = await waveContract.getAllWaves();
    console.log(allWaves);
};

main().catch((error) => {
    console.error(error);
    process.exitCode = 1;
});
```

Voc√™ nem sempre ter√° bons tutoriais como este para orient√°-lo sobre como testar seu c√≥digo. Cabe a voc√™ descobrir :

 1) O que voc√™ quer testar
 2) Como test√°-lo. Nesse caso, eu sabia que queria garantir que o saldo do contrato fosse reduzido em 0.0001 apenas no caso de um # aleat√≥rio menor que 50 ser gerado!

Ent√£o, quando executo o c√≥digo acima, aqui est√° o que recebo:

![npx hardhat run scripts/run.js](https://i.imgur.com/yD4Sn65.png)

Legal! Funciona. Quando "65" foi gerado, o usu√°rio n√£o ganhou o pr√™mio. Mas, quando 45 foi gerado, o tchauzinho venceu! E o saldo do contrato caiu exatamente 0.0001. √ìtimo :).

Cooldowns para evitar spammers
-----------------------------

Impressionante. Voc√™ tem uma maneira de enviar ETH aleatoriamente para as pessoas! Agora, pode ser √∫til adicionar uma fun√ß√£o de cooldown ao seu site para que as pessoas n√£o possam simplesmente enviar spam para voc√™. Por qu√™? Bem, talvez voc√™ simplesmente n√£o queira que eles continuem tentando ganhar o pr√™mio repetidamente mandando um tchauzinho para voc√™. Ou talvez voc√™ n√£o queira *apenas* *as* mensagens deles preenchendo seu mural de mensagens.

Confira o c√≥digo. Eu adicionei coment√°rios onde adicionei novas linhas.

Eu uso uma estrutura de dados especial chamada [mapa](https://medium.com/upstate-interactive/mappings-in-solidity-explained-in-under-two-minutes-ecba88aff96e).

```solidity
// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.0;

import "hardhat/console.sol";

contract WavePortal {
    uint256 totalWaves;
    uint256 private seed;

    /*
     * Um pouco de m√°gica, use o Google para entender o que s√£o eventos em Solidity!
     */
    event NewWave(address indexed from, uint256 timestamp, string message);

    /*
     * Crio um struct Wave.
     * Um struct √© basicamente um tipo de dados customizado onde n√≥s podemos customizar o que queremos armazenar dentro dele
     */
    struct Wave {
        address waver; // Endere√ßo do usu√°rio que deu tchauzinho
        string message; // Mensagem que o usu√°rio envio
        uint256 timestamp; // Data/hora de quando o usu√°rio tchauzinhou.
    }

    Wave[] waves;

    /*
     * Declara a vari√°vel waves que permite armazenar um array de structs.
     * Isto que me permite armazenar todos os tchauzinhos que qualquer um tenha me enviado!
     */
    mapping(address => uint256) public lastWavedAt;

    constructor() payable {
        console.log("Contrato construido!");
        /*
         * Define a semente inicial
         */
        seed = (block.timestamp + block.difficulty) % 100;
    }

    function wave(string memory _message) public {
        /*
         * Precisamos garantir que o valor corrente de timestamp √© ao menos 15 minutos maior que o √∫ltimo timestamp armazenado
         */
        require(
            lastWavedAt[msg.sender] + 15 minutes < block.timestamp,
            "Espere 15m"
        );

        /*
         * Atualiza o timestamp atual do usu√°rio
         */
        lastWavedAt[msg.sender] = block.timestamp;

        totalWaves += 1;
        console.log("%s tchauzinhou!", msg.sender);

        /*
         * Aqui √© onde eu efetivamenet armazeno o tchauzinho no array.
         */
        waves.push(Wave(msg.sender, _message, block.timestamp));

        /*
         * Gera uma nova semente para o pr√≥ximo usu√°rio que mandar um tchauzinho
         */
        seed = (block.difficulty + block.timestamp + seed) % 100;

        if (seed <= 50) {
            console.log("%s venceu!", msg.sender);

            uint256 prizeAmount = 0.0001 ether;
            require(
                prizeAmount <= address(this).balance,
                "Tentando sacar mais dinheiro que o contrato possui."
            );
            (bool success, ) = (msg.sender).call{value: prizeAmount}("");
            require(success, "Falhou em sacar dinheiro do contrato.");
        }

        /*
         * Eu adicionei algo novo aqui. Use o Google para tentar entender o que √© e depois me conte o que aprendeu em #general-chill-chat
         */
        emit NewWave(msg.sender, block.timestamp, _message);
    }

    /*
     * Adicionei uma fun√ß√£o getAllWaves que retornar√° os tchauzinhos.
     * Isso permitir√° recuperar os tchauzinhos a partir do nosso site!
     */
    function getAllWaves() public view returns (Wave[] memory) {
        return waves;
    }

    function getTotalWaves() public view returns (uint256) {
        return totalWaves;
    }
}
```

Tente executar `npx hardhat run scripts/run.js` e veja a mensagem de erro que voc√™ recebe se tentar mandar um tchauzinho duas vezes seguidas sem esperar 15 minutos üòä

üí• E √© assim que voc√™ constr√≥i cooldowns!
